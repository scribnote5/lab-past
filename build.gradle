// 외부 의존 라이브러리를 클래스 패스에 추가한다. 
buildscript {
	ext { 
		// 스프링 부트의 버전을 지정한다.
		springBootVersion = '2.2.6.RELEASE'
		// querydsl 플러그인 버전을 지정한다. 
		querydslPluginVersion = '1.0.10'
	}
	// 의존성 라이브러리를 다운받는 원격 저장소를 지정한다.  
	repositories {
		mavenCentral()
		jcenter()
		// plugin 저장소
		maven { url "https://plugins.gradle.org/m2/" } 
	}
	// 프로젝트 개발에 필요한 의존성을 선언한다.
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath ("io.spring.gradle:dependency-management-plugin:1.0.7.RELEASE")
	 	classpath("gradle.plugin.com.ewerk.gradle.plugins:querydsl-plugin:${querydslPluginVersion}")
	}
}

// 자바 플러그인을 설정한다. 
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.ewerk.gradle.plugins.querydsl'

group = 'kr.ac'
version = '0.0.1-SNAPSHOT'
// 자바 버전을 지정한다. 
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
	jcenter()
}

// 프로젝트 개발에 필요한 의존 라이브러를 선언한다. 
dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.data:spring-data-rest-hal-browser'
	
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity5'
	
	implementation 'org.projectlombok:lombok' // lombok
	implementation 'com.querydsl:querydsl-jpa' // querydsl 
    implementation 'com.querydsl:querydsl-apt' // querydsl
	implementation 'org.mapstruct:mapstruct:1.3.1.Final' // mapstruct
	
	compileOnly 'org.bgee.log4jdbc-log4j2:log4jdbc-log4j2-jdbc4:1.16' //log4jdbc
	
	runtimeOnly 'org.springframework.boot:spring-boot-devtools'
	runtimeOnly 'com.h2database:h2'
	runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
	
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.3.1.Final' 
    annotationProcessor 'org.projectlombok:lombok'
	
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	
	testAnnotationProcessor 'org.mapstruct:mapstruct-processor:1.3.1.Final'
}

def querydslSrcDir = 'src/main/generated'

querydsl {
    library = "com.querydsl:querydsl-apt"
    jpa = true
    querydslSourcesDir = querydslSrcDir
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', querydslSrcDir, 'build/generated/sources/annotationProcessor/java/main'] // mapstruct 경로 
		}
    }
}

compileQuerydsl{
    options.annotationProcessorPath = configurations.querydsl
}

configurations {
    querydsl.extendsFrom compileClasspath
}